Especificação da Sintaxe da Linguagem LALG
- Comentários na LALG: entre { } ou /* */


<programa> -> program ident <corpo> .
<corpo> -> <dc> begin <comandos> end
<dc> -> <dc_v> <mais_dc> | <dc_p> | λ
<mais_dc> -> ; <dc> | λ
<dc_v> ->  <tipo_var> : <variaveis>
<tipo_var> -> real | integer
<variaveis> -> ident <mais_var>
<mais_var> -> , <variaveis> | λ
<dc_p> -> procedure ident <parametros> <corpo_p>
<parametros> -> (<lista_par>) | λ
<lista_par> -> <tipo_var> : <variaveis> <mais_par>
<mais_par> -> ; <lista_par> | λ
<corpo_p> -> <dc_loc> begin <comandos> end
<dc_loc> -> <dc_v> <mais_dcloc> | λ
<mais_dcloc> -> ; <dc_loc> | λ
<lista_arg> -> (<argumentos>) | λ
<argumentos> -> ident <mais_ident>
<mais_ident> -> , <argumentos> | λ
<comandos> -> <comando> <mais_comandos>
<mais_comandos> -> ; <comandos> | λ

<comando> -> read (ident) |
						 write (ident) |
						 if <condicao> then <comandos> <pfalsa> $ |
						 while <condicao> do <comandos> $ |
						 ident <restoIdent>
							
<restoIdent> -> := <expressao> | <lista_arg>
<condicao> -> <expressao> <relacao> <expressao>
<relacao> -> = | <> | >= | <= | > | <
<expressao> -> <termo> <outros_termos>
<termo> -> <op_un> <fator> <mais_fatores>
<op_un> -> - | λ
<fator> -> ident | numero_int | numero_real | (<expressao>)
<outros_termos> -> <op_ad> <termo> <outros_termos> | λ
<op_ad> -> + | -
<mais_fatores> -> <op_mul> <fator> <mais_fatores> | λ
<op_mul> -> * | /
<pfalsa> -> else <comandos> | λ
